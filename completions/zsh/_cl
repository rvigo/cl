#compdef _cl cl

#you must ensure that $(brew --prefix)/share/zsh/site-functions is present in $FPATH
_cl() {
    local line
    typeset -A opt_args
    
    _arguments -C \
    "-h[Show help]" \
    ":command:_cl_subcommands" \
    "*::options:->options"
    
    case $line[1] in
        exec)
            _cl_aliases
        ;;
        share)
            _cl_share
        ;;
        config)
            _cl_config
        ;;
    esac
}

_cl_subcommands() {
    local -a commands
    commands=(
        'exec:Run your commands via CLI'
        'share:Import/Export aliases'
        'config:Configure your app'
    )
    
    _describe 'command' commands
}

_cl_config() {
    _arguments -C \
    "-q[Set the default quiet mode]"
}

_cl_exec() {
    _arguments -C  \
    "-n[The namespace in case of duplicated aliases]" \
    "-d[Dry run mode]" \
    "-q[Quiet mode]" \
    ":alias:->_cl_aliases"
}

_cl_share() {
    local curcontext="$curcontext" line state
    typeset -A opt_args
    
    _arguments -C \
    "-n[The namespace(s) to be imported from/exported to file]" \
    "-f[The location of the file to be imported from/exported to]" \
    ":mode:_cl_share_modes"
}

_cl_share_modes() {
    local -a modes
    modes=(
        'import'
        'export'
    )
    
    _describe 'mode' modes
}

_cl_aliases() {
    local -a words lines
    # removes everything until the first dot (including the dot): sed '/\..*/s/^[^.]*\.//'
    # removes everything after the first colon (including the conlon): sed 's/:.*$//'
    # removes everything after the --> (including the -->): sed 's/-->.*$//'
    # trim leading and trailing spaces: sed 's/ *$//g'
    
    lines=(${(f)"$(cl misc)"})
    for line in $lines; do
        words+="$(echo $line | sed -e '/\..*/s/^[^.]*\.//;s/:.*$//;s/-->.*$//;s/ *$//g')"
    done
    
    compadd -l -d lines -a words
}

cl-exec-widget() {
    emulate -LR zsh
    
    local -a cl=(cl misc -f)
    local fzf=(
        fzf \
        --no-multi \
        --ansi \
        --no-sort \
        --cycle \
        --reverse \
        --exit-0 \
        --height 40% \
        --preview="cl misc -d -a {1}" \
        --preview-window=right:60%:wrap
    )
    
    local choice=$($cl | $fzf)
    if [ ! -z "$choice" ]; then
        zle -U "cl exec $choice"
    fi
    zle accept-line
    zle reset-prompt
}

if [ -x "$(command -v fzf)" ]; then
    zle -N cl-exec-widget
    bindkey '^o' cl-exec-widget
fi
